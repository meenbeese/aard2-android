plugins {
    id 'com.android.application' version '8.2.1'
    id 'org.jetbrains.kotlin.android' version '1.9.22' apply true
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://jitpack.io"
    }
}

android {
    namespace "itkach.aard2"
    compileSdk 33

    defaultConfig {
        applicationId "itkach.aard2"
        minSdk 23
        targetSdk 34
        versionCode 56
        versionName "0.56"
    }

    packagingOptions {
        project("slobj").file("exclude.txt").eachLine {
            line ->
                println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias "aard2-android"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        if (System.getenv("KEYSTORE") != null) {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    lint {
        abortOnError false
    }

}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    api 'com.fasterxml.jackson.core:jackson-core:2.16.1'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.16.1'
    api 'com.github.kazy1991:FontDrawable:0.9.5'
    api project(':slobj')
    api project(':slobber')
}
